<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="3.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:bnd="http://www.ivoa.net/xml/vodml-binding/v0.9.1"
                xmlns:xsd="http://www.w3.org/2001/XMLSchema"
                xmlns:vf="http://www.ivoa.net/xml/VODML/functions"
                xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                xmlns:vo-dml="http://www.ivoa.net/xml/VODML/v1">

    <!-- this stylesheet should be included in stylesheets that want to make use of the binding
    mechanism. It should be included after the $binding variable containing the list of binding files has been set -->
    <xsl:import href="common-binding.xsl"/>

    <!--read the mapping from the bindings -->
    <xsl:variable name="mapping">
        <bnd:mappedModels>
            <xsl:for-each select="tokenize($binding,',')">
                <xsl:copy-of
                        select="document(normalize-space(.))/bnd:mappedModels/model" />
            </xsl:for-each>
        </bnd:mappedModels>
    </xsl:variable>

    <!-- load all models at start -->
    <xsl:variable name="models">
        <xsl:for-each select="$mapping/bnd:mappedModels/model">
            <xsl:choose>
                <xsl:when test="file"> <!-- prefer local file for reading defn -->
                    <xsl:choose>
                        <xsl:when test="doc-available(file)">
<!--                            <xsl:message>loading model file <xsl:value-of select="file"/></xsl:message>-->
                            <xsl:copy-of
                                    select="document(file)/vo-dml:model" />
                        </xsl:when>
                        <xsl:otherwise>
                            <xsl:message terminate="yes">cannot find file <xsl:value-of select="file"/></xsl:message>
                        </xsl:otherwise>
                    </xsl:choose>
                </xsl:when>
                <xsl:when test="url">
                    <xsl:message>opening uri  <xsl:value-of select="url"/></xsl:message>
                    <xsl:copy-of
                            select="document(url)/vo-dml:model" />
                </xsl:when>
                <xsl:otherwise>
                    <xsl:message terminate="yes">Model <xsl:value-of select="vodml-id" />has neither url nor file, hence no artifacts will be generated.</xsl:message>
                </xsl:otherwise>
            </xsl:choose>
        </xsl:for-each>
    </xsl:variable>
<!-- some important global variables -->
    <xsl:variable name="themodelname" select="/vo-dml:model/name"/>
    <xsl:variable name="vodmlauthor" select="'@author generated by https://github.com/ivoa/vo-dml tools'"/>
    <xsl:variable name="lt">&lt;</xsl:variable>
    <xsl:variable name="gt">&gt;</xsl:variable>
    <xsl:variable name="isRdbSingleInheritance" as="xsd:boolean" select="vf:isRdbSingleTable(/vo-dml:model/name)"/>
    <xsl:variable name="RdbSchemaName">
        <xsl:choose>
            <xsl:when test="$mapping/bnd:mappedModels/model[name=$themodelname]/rdb/@schema">
                <xsl:value-of select="$mapping/bnd:mappedModels/model[name=$themodelname]/rdb/@schema"/>
<!--                <xsl:message>custom tap schema =<xsl:value-of select="$mapping/bnd:mappedModels/model[name=$themodelname]/rdb/@schema"/></xsl:message>-->
            </xsl:when>
            <xsl:otherwise>
                <xsl:sequence select="$themodelname"/>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:variable>

    <xsl:variable name="isRDBUseColRef" as="xsd:boolean" select="vf:isRdbAddRef(/vo-dml:model/name)"/>
    <xsl:variable name="isRDBNaturalJoin" as="xsd:boolean" select="vf:isRdbNaturalJoin(/vo-dml:model/name)"/>
    <xsl:variable name="RdbListDelimiter" as="xsd:string" select="vf:rdbListDelimiter(/vo-dml:model/name)"/>
    <xsl:variable name="discriminatorColumnLength" as="xsd:int">
        <xsl:choose>
            <xsl:when test="$mapping/bnd:mappedModels/model[name=$themodelname]/rdb/@discriminatorColumnLength">
                <xsl:value-of select="$mapping/bnd:mappedModels/model[name=$themodelname]/rdb/@discriminatorColumnLength"/>
            </xsl:when>
            <xsl:otherwise>32</xsl:otherwise>
        </xsl:choose>
    </xsl:variable>
</xsl:stylesheet>
